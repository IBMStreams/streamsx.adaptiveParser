namespace sample;

use com.ibm.streamsx.adaptiveparser::*;

composite RegexFilterSample {
param
	expression<rstring> $data : getSubmissionTimeValue("data", 'true, 42, "text", (1427889600,657267,0)');
graph
	stream<rstring data> CSVFieldsStream = Custom() {
        logic
            onProcess : {
				submit({data = $data}, CSVFieldsStream);
            }
    }
    
	/*
	 *  AdaptiveParser expects the input attribute to be of type blob or rstring
	 *  Output SPL type generates the parser
	 *  Parameters define general behavior: delimiter is ',' and any whitespaces should be skipped (also the default)
	 *  Custom formats can be handled by custom output functions: Param and ParamN
	 */
    (stream<boolean bl, int32 it, rstring str, timestamp ts> FilteredParsedStream; stream<CSVFieldsStream> UnFilteredUnParsedStream) = AdaptiveParser(CSVFieldsStream) {
		logic
    		state : rstring filter = getSubmissionTimeValue("filter", "t.*t");
    	param
    		globalDelimiter : ",";
    		globalSkipper : whitespace;
    		quotedStrings : true;
    		tsFormat : "SPL";
    	output FilteredParsedStream:
    		str = Param({regexFilter = filter});
    		
    }
    
    () as FSink1 = FileSink(FilteredParsedStream) {
        param
            file : "/dev/stdout";
            flush : 1u;
    }
    
    () as FSink2 = FileSink(UnFilteredUnParsedStream) {
        param
            file : "/dev/stdout";
            flush : 1u;
    }
}

