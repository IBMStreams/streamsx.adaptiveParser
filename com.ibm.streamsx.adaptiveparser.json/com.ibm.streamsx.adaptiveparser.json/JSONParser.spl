namespace com.ibm.streamsx.adaptiveparser.json ;

use com.ibm.streamsx.adaptiveparser::* ;

/*
 *  AdaptiveParser output port type generates the JSON parser
 *  JSONParser parameters:
 *  	'dataAttr' - is needed to set input attribute to parse
 *  	'batch' - if bulk of tuples is expected in the input
 *  	'keyQuoted' - are the JSON keys quoted - true by default
 *  	'JSONType' - the SPL output type
 */
public composite JSONParser(input RawData ; output JSONEvent) {
param
	attribute $dataAttr;
	expression<boolean> $batch : false;
    expression<boolean> $keyQuoted : true;
	type $JSONType;

graph
    
    stream<$JSONType> JSONEvent = AdaptiveParser(RawData) {
    	param
    		batch : $batch;
    		dataAttr : $dataAttr;
    		globalDelimiter : ",";
    		globalAttrNameAsPrefix : true;
    		globalAttrNameDelimiter : ":";
    		globalAttrNameQuoted : $keyQuoted;
    		quotedStrings : true;
    		listPrefix : "[";
    		listSuffix : "]";
    		mapPrefix : "{";
    		mapSuffix : "}";
    		tuplePrefix : "{";
    		tupleSuffix : "}";
    }
}
